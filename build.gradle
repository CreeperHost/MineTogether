buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "CreeperHost"
            url = 'https://maven.creeperhost.net'
        }
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.configFile = file "project.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"

group = "${config.group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${config.mod_name}-${config.minecraft_version}"

repositories {
    mavenLocal()
    maven {
        name = "CreeperHost"
        url = 'https://maven.creeperhost.net'
    }
}

minecraft {
    version = "${config.forge_version}"
    runDir = "run"

    mappings = "snapshot_20161220"
    makeObfSourceJar = false

    replace "@VERSION@", project.version.toString()
}

configurations {
    shadow
    compile.extendsFrom shadow
}

dependencies {
    shadow 'org.nanohttpd:nanohttpd:2.3.1'
    shadow 'com.github.scribejava:scribejava-apis:6.9.0'
    shadow 'com.github.scribejava:scribejava-core:6.9.0'
    shadow("net.creeperhost:MineTogetherSessions:1.2.2") {
        transitive false
    }

    runtimeClasspath "net.covers1624:DevLogin:0.1.+"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier 'base'
}

shadowJar {
    configurations = [project.configurations.shadow]

    // Don't shadow netty, we already have it.
    dependencies {
        exclude(dependency("io.netty::"))
    }

    // Relocate fluent-hc to prevent conflicts with other mods that include it
    relocate 'com.github.scribejava', 'net.creeperhost.minetogether.com.github.scribejava'
    relocate 'fi.iki.elonen', 'net.creeperhost.minetogether.fi.iki.elonen'
    relocate 'com.fasterxml.jackson', 'net.creeperhost.minetogether.com.fasterxml.jackson'

    classifier ''
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn deobfJar

task signJar(type: Exec) {
    commandLine 'cmd', '/C', 'jarsigner -tsa http://timestamp.digicert.com -verbose -keystore NONE -storetype PKCS11 -providerClass sun.security.pkcs11.SunPKCS11 -providerArg "eToken.cfg" "build\\libs\\MineTogether-*-'+project.version.toString()+'.jar" "CreeperHost LTD"'
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}