buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.configFile = file "project.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"

group = "${config.group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${config.mod_name}-${config.minecraft_version}"

minecraft {
    version = "${config.forge_version}"
    runDir = "run"

    mappings = "snapshot_20161220"
    makeObfSourceJar = false

    replace "@VERSION@", project.version.toString()
}

configurations {
    shadow
    compile.extendsFrom shadow
}

dependencies {
    shadow 'org.kitteh.irc:client-lib:7.1.0'
    shadow 'net.engio:mbassador:1.3.1'
    shadow 'org.nanohttpd:nanohttpd:2.3.1'
    shadow 'com.github.scribejava:scribejava-apis:6.9.0'
    shadow 'com.github.scribejava:scribejava-core:6.9.0'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier 'base'
}

shadowJar {
    configurations = [project.configurations.shadow]

    // Don't shadow netty, we already have it.
    dependencies {
        exclude(dependency("io.netty::"))
    }

    // Relocate fluent-hc to prevent conflicts with other mods that include it
    relocate 'org.kitteh.irc', 'net.creeperhost.minetogether.org.kitteh.irc'
    relocate 'net.engio.mbassy', 'net.creeperhost.minetogether.net.engio.mbassy'
    relocate 'com.github.scribejava', 'net.creeperhost.minetogether.com.github.scribejava'
    relocate 'fi.iki.elonen', 'net.creeperhost.minetogether.fi.iki.elonen'
    relocate 'com.fasterxml.jackson', 'net.creeperhost.minetogether.com.fasterxml.jackson'

    classifier ''
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn deobfJar

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}