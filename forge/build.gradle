plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "minetogether.mixins.json"
    }
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")
    modApi("dev.architectury:architectury-forge:${rootProject.architectury_version}")
    modApi("net.creeperhost:polylib-forge:${rootProject.polylib_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    forgeRuntimeLibrary('org.nanohttpd:nanohttpd:2.3.1')
    forgeRuntimeLibrary('com.github.scribejava:scribejava-apis:6.9.0')
    forgeRuntimeLibrary('com.github.scribejava:scribejava-core:6.9.0')
    forgeRuntimeLibrary("net.creeperhost.minetogether:minetogether-api:${rootProject.minetogether_api_version}")
    forgeRuntimeLibrary('io.sentry:sentry:5.7.2')

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.github.scribejava:scribejava-apis:6.9.0'
    implementation 'com.github.scribejava:scribejava-core:6.9.0'
    implementation("net.creeperhost.minetogether:minetogether-api:${rootProject.minetogether_api_version}")
    implementation('io.sentry:sentry:5.7.2')

    shadowCommon 'org.nanohttpd:nanohttpd:2.3.1'
    shadowCommon 'com.github.scribejava:scribejava-apis:6.9.0'
    shadowCommon 'com.github.scribejava:scribejava-core:6.9.0'
    shadowCommon "net.creeperhost.minetogether:minetogether-api:${rootProject.minetogether_api_version}"
    shadowCommon "io.sentry:sentry:5.7.2"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    // Don't shadow netty, we already have it.
    dependencies {
        exclude(dependency("io.netty::"))
        exclude(dependency("com.google::"))
    }

    // Relocate fluent-hc to prevent conflicts with other mods that include it
    relocate 'com.github.scribejava', 'net.creeperhost.minetogether.com.github.scribejava'
    relocate 'fi.iki.elonen', 'net.creeperhost.minetogether.fi.iki.elonen'
    relocate 'com.fasterxml.jackson', 'net.creeperhost.minetogether.com.fasterxml.jackson'
    relocate 'io.sentry', 'net.creeperhost.minetogether.io.sentry'

//    classifier '' // Replace the default JAR
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName.set "${rootProject.archives_base_name}-${project.name}-${rootProject.minecraft_version}"
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
